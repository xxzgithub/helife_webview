if (IS_MODULE.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
apply plugin: 'com.jakewharton.butterknife'
apply plugin: 'maven'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion

    defaultConfig {
        if (IS_MODULE.toBoolean()) {
            applicationId "com.hilife.webview"
        }
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
                includeCompileClasspath true
            }
        }
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        sourceSets {
            main {
                if (IS_MODULE.toBoolean()) {
                    manifest.srcFile 'src/main/module/AndroidManifest.xml'
                } else {
                    manifest.srcFile 'src/main/AndroidManifest.xml'
                    //集成开发模式下排除debug文件夹中的所有Java文件
                    java {
                        exclude '**/debug/**'
                    }
                }
            }
        }

        //缓存策略
        configurations.all {
            resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
            resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "Boolean", "IS_MODULE", IS_MODULE

        }
        debug {
            buildConfigField "Boolean", "IS_MODULE", IS_MODULE
        }

    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    //sdk_public_base
    api(rootProject.ext.dependencies['sdk_public_base'])

    //dagger2
    annotationProcessor rootProject.ext.dependencies['dagger2-compiler']
    //butterknife
    annotationProcessor rootProject.ext.dependencies['butterknife-compiler']
    //圆角的图片
    implementation rootProject.ext.dependencies['roundedimageview']
    // 下拉刷新，上拉加载
    implementation rootProject.ext.dependencies['smart_refreshlayout']
    implementation rootProject.ext.dependencies['smart_refreshlayout_header']
    //小红点
    implementation rootProject.ext.dependencies['badgeview']
    implementation rootProject.ext.dependencies['lib_imagepicker']
    //融云
    implementation('cn.rongcloud.sdk:im_lib:5.1.1')
    implementation('cn.rongcloud.sdk:im_kit:5.1.1')
    implementation 'com.meiqia:meiqiasdk:3.6.6'

    implementation rootProject.ext.dependencies['lib_arms_imageloader_glide']
    implementation 'com.github.ai2101039:YLCircleImageView:v1.0.8'
    //美洽相关
    //implementation 'com.meiqia:meiqiasdk:+'
    implementation 'com.android.support:support-v4:23.1.1'
    //arouter
    annotationProcessor rootProject.ext.dependencies['arouter-compiler']
    implementation rootProject.ext.dependencies['lib_custom_views']

    implementation 'com.flyco.roundview:FlycoRoundView_Lib:1.1.4@aar'
    //研究院im
//    implementation 'com.github.tangyxgit.HLifeYzAndroid:imkit:1.0.1.8'
    implementation 'com.tencent.tbs.tbssdk:sdk:43903'

}

uploadArchives {
    repositories.mavenDeployer {
        //release地址
        repository(url: project.RELEASE_REPOSITORY_URL) {
            authentication(userName: project.USERNAME, password: project.PASSWORD)
        }
        //snapshot地址
        snapshotRepository(url: project.SNAPSHOT_REPOSITORY_URL) {
            authentication(userName: project.USERNAME, password: project.PASSWORD)
        }
        pom.groupId = "com.hilife.webview"
        pom.artifactId = "hilife_android_helife_module_webview"
        //修改版本号
        //测试开发添加SNAPSHOT,能自动识别,上线去掉SNAPSHOT,每次更新版本号加1个单位
        pom.version = rootProject.ext.version["module_message"]
        pom.packaging = "aar"
        print "包地址:\n"
        print pom.groupId + ":" + pom.artifactId + ":" + pom.version + "\n\n\n"
    }
}